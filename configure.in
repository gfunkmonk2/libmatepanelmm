f# Configure.in
#
#  This file tests for various compiler features needed to configure 
#  the gtkmm package.  Original skeleton was provided by Stephan Kulow.
#  All tests were written by Tero Pulkkinen, Mirko Streckenbach, and 
#  Karl Nelson.
#
#
#  Copyright 1999-2003 gtkmm Development Team
#  Copyright 1998 Stephan Kulow 
#
AC_INIT(libmatepanelapplet/libmatepanelappletmmconfig.h.in)

#########################################################################
#  Version and initialization
#########################################################################
LIBPANELAPPLETMM_MAJOR_VERSION=2
LIBPANELAPPLETMM_MINOR_VERSION=26
LIBPANELAPPLETMM_MICRO_VERSION=0

#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interface that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work 
#                 better
# CURRENT : REVISION : AGE
LIBPANELAPPLETMM_SO_VERSION=1:1:0

LIBPANELAPPLETMM_VERSION=$LIBPANELAPPLETMM_MAJOR_VERSION.$LIBPANELAPPLETMM_MINOR_VERSION.$LIBPANELAPPLETMM_MICRO_VERSION
LIBPANELAPPLETMM_RELEASE=$LIBPANELAPPLETMM_MAJOR_VERSION.$LIBPANELAPPLETMM_MINOR_VERSION
AC_DEFINE_UNQUOTED(LIBPANELAPPLETMM_MAJOR_VERSION, $LIBPANELAPPLETMM_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(LIBPANELAPPLETMM_MINOR_VERSION, $LIBPANELAPPLETMM_MINOR_VERSION)
AC_DEFINE_UNQUOTED(LIBPANELAPPLETMM_MICRO_VERSION, $LIBPANELAPPLETMM_MICRO_VERSION)
AC_DEFINE_UNQUOTED(LIBPANELAPPLETMM_VERSION, "$LIBPANELAPPLETMM_VERSION")
AC_SUBST(LIBPANELAPPLETMM_VERSION)
AC_SUBST(LIBPANELAPPLETMM_RELEASE)
AC_SUBST(LIBPANELAPPLETMM_SO_VERSION)

AC_CONFIG_AUX_DIR(scripts)                

dnl For automake.
VERSION=$LIBPANELAPPLETMM_VERSION
PACKAGE=libmatepanelappletmm

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl Specify a configuration file (no autoheader)
AM_CONFIG_HEADER(libmatepanelapplet/libmatepanelappletmmconfig.h)
AM_MAINTAINER_MODE
AL_ACLOCAL_INCLUDE(scripts)


#########################################################################
#  Configure arguments 
#########################################################################

#########################################################################
#  Environment Checks
#########################################################################
AC_PROG_CC
AC_PROG_CPP
AC_PROG_MAKE_SET
AC_CANONICAL_HOST
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

AL_PROG_GNU_M4(AC_MSG_ERROR([dnl
SUN m4 does not work for building gtkmm. 
Please install GNU m4.]))

AL_PROG_GNU_MAKE(AC_MSG_ERROR([dnl
SUN make does not work for building gtkmm.
Please install GNU make.]))

AL_PROG_PERL(AC_MSG_ERROR([Perl is required to build gtkmm.]))

AC_CHECK_PROGS(PERL, perl5 perl)

#########################################################################
#  Dependancy checks
#########################################################################

# Make these available to both PKG_CHECK_MODULES and the .pc.in file:
GTKMM_VERSION_NEEDED=2.4.0
AC_SUBST(GTKMM_VERSION_NEEDED)
MATECONFMM_VERSION_NEEDED=2.6.0
AC_SUBST(MATECONFMM_VERSION_NEEDED)
LIBPANELAPPLET_VERSION_NEEDED=1.1.0
AC_SUBST(LIBPANELAPPLET_VERSION_NEEDED)


PKG_CHECK_MODULES(LIBPANELAPPLETMM, gtkmm-2.4 >= $GTKMM_VERSION_NEEDED mateconfmm-2.6 >= $MATECONFMM_VERSION_NEEDED libmatepanelapplet-2.0 >= $LIBPANELAPPLET_VERSION_NEEDED)
AC_SUBST(LIBPANELAPPLETMM_CFLAGS)
AC_SUBST(LIBPANELAPPLETMM_LIBS)

GLIBMM_LIBDIR=`pkg-config --variable=libdir glibmm-2.4`
GMM_PROCDIR=$GLIBMM_LIBDIR/glibmm-2.4/proc
AC_SUBST(GMM_PROCDIR)
GMM_PROC=$GMM_PROCDIR/gmmproc
AC_SUBST(GMM_PROC)

#########################################################################
#  C++ checks
#########################################################################
AC_PROG_CXX
AC_LANG_CPLUSPLUS


# Create a list of input directories for Doxygen.  We have to check wether
# we are building in the source directory, because Doxygen doesn't like
# duplicated directories in the input list.
LIBPANELAPPLETMM_DOXYGEN_INPUT=''
gtkmm_srcdir=`cd "$srcdir" && pwd`
gtkmm_builddir=`pwd`
for sublib in libmatepanelapplet
do
  LIBPANELAPPLETMM_DOXYGEN_INPUT="$LIBPANELAPPLETMM_DOXYGEN_INPUT $gtkmm_builddir/$sublib/$sublib""mm/"
  test "x$gtkmm_builddir" = "x$gtkmm_srcdir" || \
    LIBPANELAPPLETMM_DOXYGEN_INPUT="$LIBPANELAPPLETMM_DOXYGEN_INPUT $gtkmm_srcdir/$sublib/$sublib""mm/"
done
AC_SUBST(LIBPANELAPPLETMM_DOXYGEN_INPUT)



# Dummy conditional just to make automake-1.4 happy.
# We need an always-false condition in docs/Makefile.am.
AM_CONDITIONAL(LIBPANELAPPLETMM_FALSE,[false])

# HACK:  Assign a dummy in order to prevent execution of autoheader by the
# maintainer-mode rules.  That would fail since we aren't using autoheader.
AUTOHEADER=':'


AC_OUTPUT([
  Makefile
  libmatepanelappletmm.spec
  
  libmatepanelapplet/Makefile
    libmatepanelapplet/libmatepanelappletmm-2.6.pc
    libmatepanelapplet/libmatepanelappletmm-2.6-uninstalled.pc
    libmatepanelapplet/src/Makefile
    libmatepanelapplet/libmatepanelappletmm/Makefile
      libmatepanelapplet/libmatepanelappletmm/private/Makefile

  tools/Makefile
    tools/extra_defs_gen/Makefile
    tools/m4/Makefile
  
  examples/Makefile
    examples/simple/Makefile
    examples/mateconf/Makefile

  scripts/Makefile

  docs/Makefile
  docs/reference/Makefile
    docs/reference/Doxyfile
])
